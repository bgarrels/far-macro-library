;; Макросы для быстрого поиска в редакторе. (с) SimSU (i) Maximus5 - EditFindLeap
;; Поиск ведется с учетом текущего языка (если текущая раскладка не английская, то используем xlat).
;; Использование: зажав Правый Контрол (RCtrl) набираем текст - получаем быстрое позиционирование,
;; не отпуская Правый Контрол (RCtrl), нажав BackSpace отменим последний введенный символ,
;; не отпуская Правый Контрол (RCtrl), нажав Enter спозиционируемся на следующее вхождение.
;; Поиск прекращается при отпускании Правого Контрола (RCtrl).

const MacroLib = 0x424C434D

macro
  Name="SimSU_IncSearch"
  Descr="Editor: Быстрый поиск. (с) SimSU"
  Key="RCtrl1 RCtrl2 RCtrl3 RCtrl4 RCtrl5 RCtrl6 RCtrl7 RCtrl8 RCtrl9 RCtrl0"
  Key="RCtrlQ RCtrlW RCtrlE RCtrlR RCtrlT RCtrlY RCtrlU RCtrlI RCtrlO RCtrlP RCtrl[ RCtrl]"
  Key="RCtrlA RCtrlS RCtrlD RCtrlF RCtrlG RCtrlH RCtrlJ RCtrlK RCtrlL RCtrl; RCtrl'"
  Key="RCtrlZ RCtrlX RCtrlC RCtrlV RCtrlB RCtrlN RCtrlM RCtrl, RCtrl."
  Key="RCtrlSpace RCtrl= RCtrlShift= RCtrl- RCtrlShift- RCtrl` RCtrlShift`"
  Key="RCtrlShift1 RCtrlShift2 RCtrlShift3 RCtrlShift4 RCtrlShift5 RCtrlShift6 RCtrlShift7 RCtrlShift8 RCtrlShift9 RCtrlShift0"
  Area="Editor"
{{
//  %s=akey(1);
  %s=key(#Akey);
  %s=replace(%s,"RCtrl","");
  $IF (%s=="Space") %s=" "; $END
  $IF (%s=="Shift=") %s="+"; $END
  $IF (%s=="Shift-") %s="_"; $END
  $IF (%s=="Shift0") %s=")"; $END
  $IF (%s=="Shift9") %s="("; $END
  $IF (%s=="Shift8") %s="*"; $END
  $IF (%s=="Shift7") %s="&"; $END
  $IF (%s=="Shift6") %s="^"; $END
  $IF (%s=="Shift5") %s="%"; $END
  $IF (%s=="Shift4") %s="$"; $END
  $IF (%s=="Shift3") %s="#"; $END
  $IF (%s=="Shift2") %s="@"; $END
  $IF (%s=="Shift1") %s="!"; $END
  $IF (%s=="Shift`") %s="~"; $END
  $IF (!%%SimSUIncSeachXlat && kbdlayout(0)!=0x4090409) %%SimSUIncSeachXlat=1; $END //Проверяем язык ввода, если не английский будем транслитерировать %).
  $IF (%%SimSUIncSeachXlat)
    %s=xlat("Z"+%s); // магия Xlat что бы искалось [];',.` - хъжэбюё
    %s=substr(%s,1);
  $END
  %%SimSUIncSeachStr=%%SimSUIncSeachStr+%s;
  CtrlU CtrlHome
  F7 CtrlY print(%%SimSUIncSeachStr)
  $IF (%%SimSUIncSeachFOpt==0) //Проводим настройку поиска с сохранением предыдущих настроек.
    %o="";
    Tab %o=%o+"\nc:"+itoa(dlg.getvalue(dlg.getvalue(0,6),0)); Subtract
    Tab %o=%o+"\nw:"+itoa(dlg.getvalue(dlg.getvalue(0,6),0)); Subtract
    Tab %o=%o+"\nv:"+itoa(dlg.getvalue(dlg.getvalue(0,6),0)); Subtract
    Tab %o=%o+"\ng:"+itoa(dlg.getvalue(dlg.getvalue(0,6),0)); Subtract
    Tab %o=%o+"\nl:"+itoa(dlg.getvalue(dlg.getvalue(0,6),0)); Add
    %o=%o+"\n::";
    %%SimSUIncSeachFOpt=%o;
  $END
  Enter
  $IF (Dialog) Esc %end=1; $END
  F7 CtrlY
  CtrlDown $IF (ItemCount>1) ShiftDel Esc $ELSE ShiftDel $END //Чистим историю.
  Enter
  $IF (%end) %end=0; beep() callplugin(#%Macrolib,"call SimSU_IncSearchCE") $END
}}

macro
  Name="SimSU_IncSearchBeg"
  Descr="Editor: Быстрый поиск (Инициализация). (с) SimSU"
  Key="RCtrl:Down"
  Area="Editor"
{{
  %%SimSUIncSeachStr="";
  %%SimSUIncSeachXlat=0;
  %%SimSUIncSeachFOpt=0;
  #Include "%%SimSU_EditSel_Save.fmi" %%SimSUIncSeachSelect=%SimSUBlockPos;
}}

macro
  Name="SimSU_IncSearchEnd"
  Descr="Editor: Быстрый поиск (Финализация). (с) SimSU"
  Key="RCtrl:Up"
  Area="Editor"
{{
  %%SimSUIncSeachStr="";
  %%SimSUIncSeachXlat=0;
  CtrlU
  F7 CtrlY
  $IF (%%SimSUIncSeachFOpt!=0) //Восстанавливаем настройки.
    %o=%%SimSUIncSeachFOpt;
    Tab $IF (atoi(substr(%o,index(%o,"\nc:")+3,1))) Add $ELSE Subtract $END
    Tab $IF (atoi(substr(%o,index(%o,"\nw:")+3,1))) Add $ELSE Subtract $END
    Tab $IF (atoi(substr(%o,index(%o,"\nv:")+3,1))) Add $ELSE Subtract $END
    Tab $IF (atoi(substr(%o,index(%o,"\ng:")+3,1))) Add $ELSE Subtract $END
    Tab $IF (atoi(substr(%o,index(%o,"\nl:")+3,1))) Add $ELSE Subtract $END
    %%SimSUIncSeachFOpt=0;
  $End
  Enter
  #Include "%%SimSU_EditPos_Save.fmi" %SimSUBlockPos=%%SimSUIncSeachSelect;  #Include "%%SimSU_EditSel_Restore.fmi"  #Include "%%SimSU_EditPos_Restore.fmi"
}}

macro
  Name="SimSU_IncSearchCE"
  Descr="Editor: Быстрый поиск (Коррекция). (с) SimSU"
  Key="RCtrlBS"
  Area="Editor"
{{
  $IF (len(%%SimSUIncSeachStr)>0)
    %%SimSUIncSeachStr=substr(%%SimSUIncSeachStr,0,len(%%SimSUIncSeachStr)-1);
    callplugin(#%Macrolib,"call SimSU_IncSearch")
  $ELSE
    beep()
  $END
}}

macro
  Name="SimSU_IncSearchNext"
  Descr="Editor: Быстрый поиск (Дальше). (с) SimSU"
  Key="RCtrlEnter"
  Area="Editor"
{{
  $IF (len(%%SimSUIncSeachStr)>0)
    CtrlU F7 CtrlY print(%%SimSUIncSeachStr) Enter
    $IF (Dialog) Esc %end=1; $END
    F7 CtrlY
    CtrlDown $IF (ItemCount>1) ShiftDel Esc $ELSE ShiftDel $END //Чистим историю.
    Enter
    $IF (%end) %end=0; beep() $END
  $END
}}
;; http://code.google.com/p/simsufar/