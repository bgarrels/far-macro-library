
// Собранный на скорую руку "Leap" для EditFind. (с) Maximus5
// Поиск идет одновременно по двум языкам через xlat.
//
// Как пользоваться:
// нажать RAlt и не отпуская набирать текст
// при отпускании RAlt буфер поиска сбрасывается
//
// Чего хочется от EditFind
// - установку флажков хорошо бы через callplugin сделать, например
//   callplugin(#%EditFind,"Case:0;Mode:Substring;Reverse:0")
// - возможность звать поиск плагина напрямую (с установленными флажками), через callplugin, например
//   callplugin(#%EditFind,"FindText:"+%%EditFindLeap)

const EditFind = 0x444e4645
// Set to 0 to disable xlat(two language) search with RegExp
const UseXLat = 1

macro
  Descr="Leap search for EditFind"
  Area="Editor"
  Bind="RAltA RAltB RAltC RAltD RAltE RAltF RAltG RAltH RAltI RAltJ RAltK RAltL RAltM RAltN RAltO RAltP RAltQ RAltR RAltS RAltT RAltU RAltV RAltW RAltX RAltY RAltZ"
  Bind="RAlt[ RAlt] RAlt; RAlt' RAlt` RAlt, RAlt."
  Bind="RAlt1 RAlt2 RAlt3 RAlt4 RAlt5 RAlt6 RAlt7 RAlt8 RAlt9 RAlt0"
  Bind="RAlt- RAltShift- RAltSpace"
  DisableOutput=1
{{
  %s = substr(Key(#AKey), 4);
  $if (%s=="Shift-")  %s = "_";  $end
  $if (%s=="Space")  %s = " ";  $end
  %%EditFindLeap=%%EditFindLeap+%s;
  F11
  $if (menu.Select("Editor Find",2)<=0)
    %%EditFindLeap="";
    MsgBox("Editor Find","Plugin was not found in list!",1)
    $Exit
  $end
  Enter Enter // Find...
  // Enter current search text
  $if (#%UseXLat==1)
    %x=xlat("Z"+%%EditFindLeap); //магия: xlat лучше если строка начинается с буквы.
    %x=substr(%x,1);             //избавляемся от фиктивной буквы.
    $if (%x==%%EditFindLeap)
      %find=%%EditFindLeap;
    $else
      %find="(\\Q"+%%EditFindLeap+"\\E)|(\\Q"+%x+"\\E)";
    $end
  $else
    %find=%%EditFindLeap;
  $end
  print(%find)
  // Default search options
  $if (len(%%EditFindLeap)==1)
    Tab $IF (!%%EditFindSave) %%EditFindCase=dlg.getvalue(dlg.getvalue(0,6),0); $END Subtract // и запоминаем настройки.
    $if (#%UseXLat==1)
      Down $IF (!%%EditFindSave) %%EditFindType=dlg.getvalue(dlg.getvalue(0,6),0); $END Down Down Space
    $else
      Down $IF (!%%EditFindSave) %%EditFindType=dlg.getvalue(dlg.getvalue(0,6),0); $END Space
    $end
    Tab $IF (!%%EditFindSave) %%EditFindRevr=dlg.getvalue(dlg.getvalue(0,6),0); $END Subtract
    %%EditFindSave=1;
  $end
  //Enter // Execute find from current pos
  Tab Tab Enter
  // When string not found
  $if (Dialog)
    %%EditFindLeap=""; // reset leap search string
    $WHILE (Dialog) %mm=mmode(1,0); %VK=waitkey(0,0); eval(%VK) mmode(1,%mm) $END //Ждем закрытия диалога.
  $end
  F11 menu.select("Editor Find",2) Enter Enter CtrlY CtrlDown $IF (ItemCount>1) ShiftDel Esc $Else ShiftDel $End //чистим историю.
  Esc
}}

macro
  Descr="Leap search for EditFind - reset search buffer/clear marks"
  Area="Editor"
  key="RAltBS"
{{
  %%EditFindLeap="";
  callplugin(#%EditFind,10)
  CtrlU
  $IF (%%EditFindSave) //Восстанавливаем настройки.
    F11 menu.select("Editor Find",2) Enter Enter
    CtrlY
    Tab $IF (%%EditFindCase) Add $ELSE Subtract $END
    Down $REP(%%EditFindType-1) Down $END Space
    Tab $IF (%%EditFindRevr) Add $ELSE Subtract $END
    Enter
    %%EditFindSave=0;
  $END
}}

macro
  Descr="Leap search for EditFind - reset search buffer"
  Area="Editor"
  key="RAlt:Up"
{{
  %%EditFindLeap="";
  //callplugin(#%EditFind,10)
  //CtrlU
}}
