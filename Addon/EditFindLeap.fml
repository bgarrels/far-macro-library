
// Собранный на скорую руку "Leap" для EditFind.
// Поиск идет одновременно по двум языкам через xlat.
//
// Как пользоваться:
// нажать RAlt и не отпуская набирать текст
// при отпускании RAlt буфер поиска сбрасывается
//
// Чего пока нет
// - некоторые русские буквы игнорируются (хъжэю)
// - расчитан на английский интерфейс
//
// Чего хочется от EditFind
// - установку флажков хорошо бы через callplugin сделать, например
//   callplugin(#%EditFind,"Case:0;Mode:Substring;Reverse:0")
// - возможность звать поиск плагина напрямую (с установленными флажками), через callplugin, например
//   callplugin(#%EditFind,"FindText:"+%%EditFindLeap)

include "Consts.fmi"

// Set to 0 to disable xlat(two language) search with RegExp
const UseXLat = 1

macro
  Descr="Leap search for EditFind"
  Area="Editor"
  Bind="RAltA RAltB RAltC RAltD RAltE RAltF RAltG RAltH RAltI RAltJ RAltK RAltL RAltM RAltN RAltO RAltP RAltQ RAltR RAltS RAltT RAltU RAltV RAltW RAltX RAltY RAltZ"
  Bind="RAlt1 RAlt2 RAlt3 RAlt4 RAlt5 RAlt6 RAlt7 RAlt8 RAlt9 RAlt0"
  Bind="RAlt. RAlt- RAltShift-"
  DisableOutput=1
{{
  %s = substr(Key(#AKey), 4);
  $if (substr(%s,0,5)=="Shift")
    %s = substr(%s,-1,1);
    $if (%s=="-") %s = "_"; $end
  $end
  $if (%s==".") %s=@"\."; $end
  %%EditFindLeap=%%EditFindLeap+%s;
  F11
  $if (menu.Select("Editor Find",2)<=0)
    %%EditFindLeap="";
    MsgBox("Editor Find","Plugin was not found in list!",1)
    $Exit
  $end
  Enter Enter // Find...
  // Enter current search text
  $if (#%UseXLat==1)
    %x=xlat(%%EditFindLeap);
    $if (%x==%%EditFindLeap)
      %find=%%EditFindLeap;
    $else
      %find="("+%%EditFindLeap+")|("+%x+")";
    $end
  $else
    %find=%%EditFindLeap;
  $end
  print(%find)
  // Default search options
  $if (len(%%EditFindLeap)==1)
    AltC Subtract
    $if (#%UseXLat==1)
      AltR // Search with regular expressions
    $else
      AltS // search substring
    $end
    AltV Subtract
  $end
  
  //Enter // Execute find from current pos
  AltE // Execute find from file beginning

  // When string not found
  $if (Dialog)
    %%EditFindLeap=""; // reset leap search string
  $end
}}

macro
  Descr="Leap search for EditFind - reset search buffer/clear marks"
  Area="Editor"
  key="RAltBS"
{{
  %%EditFindLeap="";
  callplugin(#%EditFind,10)
  CtrlU
}}

macro
  Descr="Leap search for EditFind - reset search buffer"
  Area="Editor"
  key="RAlt:Up"
{{
  %%EditFindLeap="";
  //callplugin(#%EditFind,10)
  //CtrlU
}}
