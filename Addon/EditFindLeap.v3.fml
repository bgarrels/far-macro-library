
//
// Инкрементальный поиск для плагина EditFind. (с) Maximus5, SimSU, Max Rusov
//
// Как пользоваться:
//
// Нажать RAlt и не отпуская набирать текст. Поиск идет одновременно по двум языкам через xlat.
// RAltBS - Удалить последний набранный символ.
// RAlt   - Сбросить найденный текст
// Для продолжения поиска вперед/назад используются соответствующие команды плагина EdtFind
//
// Требуется: Far3, MacroLib 1.0.13, EdtFind v.8+
// http://code.google.com/p/far-plugins/wiki/EdtFind
//


// - ID для Far2
//const EdtFind  = 0x444E4645
// - ID для Far3 (через враппер)
const EdtFind = "8EF28982-957E-4BCE-AD73-7E67DB443969"
const RemoveHilight = 8

;; Set to 0 to disable xlat(two language) search with RegExp
const UseXLat = 1
;;
const HilightFrom = 2


macro Descr = "Edt Find: Leap search begin" Area = "Editor" Key = "RAlt:Down"
{{
  %%EditFindLeap = "";
}}

farmacro
  Descr="Edt Find: Leap search continue"
  Area="Editor"
  Bind="RAltA RAltB RAltC RAltD RAltE RAltF RAltG RAltH RAltI RAltJ RAltK RAltL RAltM RAltN RAltO RAltP RAltQ RAltR RAltS RAltT RAltU RAltV RAltW RAltX RAltY RAltZ"
  Bind="RAlt1 RAlt2 RAlt3 RAlt4 RAlt5 RAlt6 RAlt7 RAlt8 RAlt9 RAlt0"
  Bind="RAlt[ RAlt] RAlt; RAlt' RAlt` RAlt. RAlt, RAlt- RAltShift_ RAlt= RAltShift= RAltShift`"
  Bind="RAltShift1 RAltShift2 RAltShift3 RAltShift4 RAltShift5 RAltShift6 RAltShift7 RAltShift8 RAltShift9 RAltShift0"
  Bind="RAltBS RAltSpace"
{{
  $if (!callplugin(#%EdtFind, ""))
//  MsgBox("Error", "EdtFind plugin not installed")
    $exit
  $end
  
  //%s = substr(Key(#AKey), 4); // Far2
  %s = substr(Key(akey(0,0)), 4); // Far3

  %Opt = "Loop:0 Error:0 ";

  $if (%s == "BS")
    %%EditFindLeap = substr(%%EditFindLeap, 0, -1);
    //%%EditFindLeap = substr(%%EditFindLeap, 0, len(%%EditFindLeap)-1); //Что бы в 1803 работало.
    $if (%%EditFindLeap == "")
      CallPlugin(#%EdtFind, #%RemoveHilight)
      $exit
    $end
  $else
    $if (%s == "Space") %s = " "; $end
    $if (%%EditFindLeap == "")
      BM.add()
      %Opt = %Opt + "Entire:1 ";
    $end

    $if (%s == "Shift_") 
      %s = "_"; 
    $else $if (%s == "Shift=") 
      %s = "+"; 
    $else $if (%s == "Shift1") 
      %s = "!"; 
    $else $if (%s == "Shift2") 
      %s = "@"; 
    $else $if (%s == "Shift3") 
      %s = "#"; 
    $else $if (%s == "Shift4") 
      %s = "$"; 
    $else $if (%s == "Shift5") 
      %s = "%"; 
    $else $if (%s == "Shift6") 
      %s = "^"; 
    $else $if (%s == "Shift7") 
      %s = "&"; 
    $else $if (%s == "Shift8") 
      %s = "*"; 
    $else $if (%s == "Shift9") 
      %s = "("; 
    $else $if (%s == "Shift0") 
      %s = ")"; 
    $else $if (%s == "Shift`") 
      %s = "~"; 
    $end $end $end $end $end $end $end $end $end $end $end $end $end
    %%EditFindLeap = %%EditFindLeap + %s;
  $end

  $if (len(%%EditFindLeap) < #%HilightFrom)
    %Opt = %Opt + "Highlight:0 ";
  $end

  %Find = %%EditFindLeap;
  $if (#%UseXLat == 1)
    %XFind = xlat("Z"+%Find);  // Помогаем определиться xlat,
    %XFind = substr(%XFind,1); // а то не находим текст начинающийся с хъжэбю
    $if (%XFind != %Find)
      %Find = "(\\Q" + %Find + "\\E)|(\\Q" + %XFind + "\\E)";
      %Opt = %Opt + "RegExp:1 ";
    $end
  $end

  $if (!callplugin(#%EdtFind, %Opt + "Find " + %Find))
    %%EditFindLeap = substr(%%EditFindLeap, 0, -1);

    %Find = %%EditFindLeap;
    %Opt = "";
    $if (#%UseXLat == 1)
      %XFind = xlat(%Find);
      $if (%XFind != %Find)
        %Find = "(\\Q" + %Find + "\\E)|(\\Q" + %XFind + "\\E)";
        %Opt = %Opt + "RegExp:1 ";
      $end
    $end
    callplugin(#%EdtFind, %Opt + "Mark " + %Find)

    Beep(0x10)
  $end
}}


macro
  Descr="Edt Find: Leap search cancel"
  Area="Editor"
  Key="RAlt:Release"
{{
//  MsgBox("","Release")
  CallPlugin(#%EdtFind, #%RemoveHilight)
  CtrlU
}}
